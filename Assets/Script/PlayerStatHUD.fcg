import "StdLibrary.fcc" as stdLibrary
import "EditorGenLib.fcc" as editorGenLib
import "Hud.fcc" as Hud
import "./HUDUtil.fcg" as HUDUtil
import Resources from "EditorGenLib.fcc"
import MyResources from "EditorGenLib.fcc"
import "./PlayerStat.fcg" as PlayerStat
import "Map.fcc" as Map
import "Convert.fcc" as Convert
import "Strings.fcc" as Strings
import "List.fcc" as List
import "Avatar.fcc" as Avatar

graph PlayerStatHUD {
    _statHUD entity<CustomHud>
    _isBtnConfirmActive bool
    _statTypeBtnIncMap Map<int, entity<HudWidget>>
    _btnStatTypeIncMap Map<string, int>

    _statTypeBtnDecMap Map<int, entity<HudWidget>>
    _btnStatTypeDecMap Map<string, int>
    _statTypeTxtMap Map<int, entity<HudWidget>>

    _hasInit bool
    
    func InitStatHUD(){
        if(_statHUD == nil){
            _statHUD = HUDUtil.CreateCustomHUD(thisEntity<Player>, EResource_UI.PLAYER_STAT, 99)
            _statHUD<CustomHud>.Visibility = false
            SetStatTypeMap()
            SetAvatar()
        }
    }

    func SetStatTypeMap(){
        _statTypeBtnIncMap = Map<int, entity<HudWidget>>{}
        _statTypeBtnDecMap = Map<int, entity<HudWidget>>{}
        _statTypeTxtMap = Map<int, entity<HudWidget>>{}
        _btnStatTypeDecMap = Map<string, int>{}
        _btnStatTypeIncMap = Map<string, int>{}

        _statTypeBtnIncMap[StatType.ATK] = GetStatHUDWidget(EResource_UI_PLAYER_STAT.BTN_INCREASE_ATK)
        _statTypeBtnIncMap[StatType.MAX_HP] = GetStatHUDWidget(EResource_UI_PLAYER_STAT.BTN_INCREASE_HP)

        _statTypeBtnDecMap[StatType.ATK] = GetStatHUDWidget(EResource_UI_PLAYER_STAT.BTN_DECREASE_ATK)
        _statTypeBtnDecMap[StatType.MAX_HP] = GetStatHUDWidget(EResource_UI_PLAYER_STAT.BTN_DECREASE_HP)

        var statTypeList = GetAllKeys(_statTypeBtnIncMap)
        for i = 0, List.Length(statTypeList), 1{
            var statType = statTypeList[i] as int
            var btnInc = _statTypeBtnIncMap[statType] as entity<HudWidget>
            var btnIncID = btnInc<HudWidget>.UniqueID
            var btnDec = _statTypeBtnDecMap[statType] as entity<HudWidget>
            var btnDecID = btnDec<HudWidget>.UniqueID
            _btnStatTypeDecMap[btnDecID] = statType
            _btnStatTypeIncMap[btnIncID] = statType
        }

        _statTypeTxtMap[StatType.ATK] = GetStatHUDWidget(EResource_UI_PLAYER_STAT.TXT_ATK)
        _statTypeTxtMap[StatType.MAX_HP] = GetStatHUDWidget(EResource_UI_PLAYER_STAT.TXT_HP)
    }

    func SetAvatar() {
        var avatar = GetStatHUDWidget(EResource_UI_PLAYER_STAT.AVATAR)
        var name = GetStatHUDWidget(EResource_UI_PLAYER_STAT.USERNAME)

        avatar<HudProfileInfo>.AccountID = thisEntity<Player>.UserUID
        name<HudLabelWidget>.Text = thisEntity<Player>.NickName
    }
    
    func DisplayStatHUD(){
        InitStatHUD()
        ResetStatHUD()
        if(!_statHUD<CustomHud>.Visibility){
            _statHUD<CustomHud>.Visibility = true
        }
    }

    func ResetStatHUD(){
        UpdateTxtStatHUD()
        UpdateBtnStatHUD()
    }

    func UpdateTxtStatHUD(){
        var statTypeList = GetAllKeys(thisEntity<PlayerStat>._statPointAdjustmentMap)

        for i = 0, List.Length(statTypeList), 1{
            var statType = statTypeList[i]
            var statPoint = thisEntity<PlayerStat>._statPointMap[statType]
            var statPointAdjustment = thisEntity<PlayerStat>._statPointAdjustmentMap[statType]
            var txt = _statTypeTxtMap[statType]
            // LogInfo("[PlayerStatHUD] statPoint: " + statPoint)
            // LogInfo("[PlayerStatHUD] statPointAdjustment: " + statPointAdjustment)
            txt<HudLabelWidget>.Text = ToString(statPoint + statPointAdjustment)

            if(statPointAdjustment > 0){
                txt<HudLabelWidget>.Color = ColorRGB(0, 255, 0)
            } else {
                txt<HudLabelWidget>.Color = ColorRGB(255, 255, 255)
            }
        }
        var txtRemaining = GetStatHUDWidget(EResource_UI_PLAYER_STAT.TXT_REMAIN)
        txtRemaining<HudLabelWidget>.Text = ToString(thisEntity<PlayerStat>._adjustingRemainingStatPoint)
    }

    func UpdateBtnStatHUD(){
        var statTypeList = GetAllKeys(thisEntity<PlayerStat>._statPointAdjustmentMap)
        var adjustingRemainingStatPoint = thisEntity<PlayerStat>._adjustingRemainingStatPoint
        var btnConfirm = GetStatHUDWidget(EResource_UI_PLAYER_STAT.BTN_CONFIRM)

        _isBtnConfirmActive = false
        for i = 0, List.Length(statTypeList), 1{
            var statType = statTypeList[i]
            var statPointAdjustment = thisEntity<PlayerStat>._statPointAdjustmentMap[statType]
            var btnInc = _statTypeBtnIncMap[statType]
            var btnDec = _statTypeBtnDecMap[statType]

            if(adjustingRemainingStatPoint > 0){
                btnInc<HudButtonWidget>.SpriteName = Resources.Texture[EResource_Texture.BTN_MID_PRESS]
            } else {
                btnInc<HudButtonWidget>.SpriteName = Resources.Texture[EResource_Texture.BTN_DISABLE]
            }

            if(statPointAdjustment > 0){
                btnDec<HudButtonWidget>.SpriteName = Resources.Texture[EResource_Texture.BTN_MID_PRESS]
                _isBtnConfirmActive = true
            } else{
                btnDec<HudButtonWidget>.SpriteName = Resources.Texture[EResource_Texture.BTN_DISABLE]
            }
        }
        if(_isBtnConfirmActive){
            btnConfirm<HudButtonWidget>.SpriteName = Resources.Texture[EResource_Texture.BTN_MID_PRESS]
        } else {
            btnConfirm<HudButtonWidget>.SpriteName = Resources.Texture[EResource_Texture.BTN_DISABLE]
        }
    }

    func CloseStatHUD(){
        if(_statHUD != nil){
            _statHUD<CustomHud>.Visibility = false
        }
    }

    func DeleteStatHUD(){
        DeleteEntity(_statHUD)
    }

    func SetConfirmBtnActivity(isActive bool){
        var btnConfirm  = GetStatHUDWidget(EResource_UI_PLAYER_STAT.BTN_CONFIRM)

        if(isActive){
            btnConfirm<HudButtonWidget>.SpriteName = Resources.Texture[EResource_Texture.BTN_MID_PRESS]            
            _isBtnConfirmActive = true
            return
        }

        if(!isActive){
            btnConfirm<HudButtonWidget>.SpriteName = Resources.Texture[EResource_Texture.BTN_DISABLE]
            _isBtnConfirmActive = false
            return
        }
    }


    func ClickButton(button entity<HudButtonWidget>){
        var statType = 0
        var isDecreasing = false
        if(ContainKey(_btnStatTypeDecMap, button<HudWidget>.UniqueID)){
            statType = _btnStatTypeDecMap[button<HudWidget>.UniqueID] as int
            isDecreasing = true
        } else {
            statType = _btnStatTypeIncMap[button<HudWidget>.UniqueID] as int
        }

        // LogInfo("[PlayerStatHUD] statType: " + statType + " " + _btnStatTypeIncMap[button<HudWidget>.UniqueID] as int + " " +_btnStatTypeDecMap + " " + _statTypeBtnDecMap + " " + button<HudWidget>.UniqueID)

        // No temporary stat left and is not decreasing remaining point
        if(thisEntity<PlayerStat>._adjustingRemainingStatPoint <= 0 && !isDecreasing){
            LogInfo("[PlayerStatHUD] No more stat point remaining")
            ShowTips(thisEntity<Player>, MyResources.Localizations["NO_REMAINING_STAT_POINT"], ColorRGB(255, 255, 255), 3000)
            return
        }
        // Is current stat type not adjusted and is decreasing remaining point
        LogInfo("[PlayerStatHUD] IsStatNotAdjusted " + thisEntity<PlayerStat>.IsStatNotAdjusted(statType))
        if(thisEntity<PlayerStat>.IsStatNotAdjusted(statType) && isDecreasing){
            LogInfo("[PlayerStatHUD] Can't reduce anymore")
            return
        }

        thisEntity<PlayerStat>.AdjustStatPointAdjustmentMap(statType, 1, isDecreasing)
        UpdateTxtStatHUD()
        UpdateBtnStatHUD()
    }

    func ClickConfirmBtn(){
        if(!_isBtnConfirmActive){
            ShowTips(thisEntity<Player>, MyResources.Localizations["BTN_CONFIRM_NOT_ACTIVE"], ColorRGB(255, 255, 255), 3000)
            return
        }

        // TODO: Implement click confirm btn
        thisEntity<PlayerStat>.ConfirmStatAdjustment()
        ResetStatHUD()
    }

    func GetStatHUDWidget(eResourceUIPlayerStat string) entity<HudWidget>{
        return GetWidgetFromCustomHud(thisEntity<Player>, _statHUD, Resources.UI_PLAYER_STAT[eResourceUIPlayerStat])
    }
}