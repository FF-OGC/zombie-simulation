import "StdLibrary.fcc" as stdLibrary
import "EditorGenLib.fcc" as editorGenLib
import "LevelObject.fcc" as levelobject
import "Convert.fcc" as Convert
import "./PlayerData.fcg" as PlayerData
import "List.fcc" as List
import "Map.fcc" as Map
import "./PlayerStatHUD.fcg" as PlayerStatHUD
import "./PlayerLevel.fcg" as PlayerLevel
import "./PlayerDefaultHUD.fcg" as PlayerDefaultHUD
import "./Enum.fcg" as Enum
import "Items.fcc" as Items
import "Animation.fcc" as Animation

graph PlayerStat {
    ORIGINAL_MAX_HP int = 200
    ORIGINAL_MAX_EP int = 200
    ORIGINAL_ATK int = 10

    _hasInit bool
    _statList List<int> = List<int>{StatType.ATK, StatType.MAX_HP}
    _statMap Map<int, int>
    _remainingStatPoint int
    _adjustingRemainingStatPoint int
    _statPointMap Map<int, int>
    _statPointAdjustmentMap Map<int, int>

    func InitPlayerStat(){
        // Check for stored data first before initialize
        if(!_hasInit){
            SetStatMap()
            _remainingStatPoint = thisEntity<PlayerLevel>.GetLvl()
            UpdateStatMap()
            SetStatPointAdjustmentMap()
            _hasInit = true
        }
    }

    func BeginPlayerStat(){
        InitPlayerStat()
        ResetPlayerStat()
        thisEntity<PlayerStatHUD>.DisplayStatHUD()
    }

    func ResetPlayerStat(){
        UpdateRemainingStatPoint()
        _adjustingRemainingStatPoint = _remainingStatPoint
        ResetStatPointAdjustmentMap()
        thisEntity<PlayerStatHUD>.ResetStatHUD()
    }

    func EndPlayerStat(){
        thisEntity<PlayerStatHUD>.CloseStatHUD()
    }

    func SetStatMap(){
        if(_statMap == nil){
            _statMap = Map<int, int>{}
            _statMap[StatType.KILL_COUNT] = 0
            _statMap[StatType.ATK] = ORIGINAL_ATK
            _statMap[StatType.MAX_HP] = ORIGINAL_MAX_HP
        }
    }

    func SetStatPointAdjustmentMap(){
        if(_statPointAdjustmentMap == nil){
            _statPointAdjustmentMap = Map<int, int>{}
        }
    }

    func ResetStatPointAdjustmentMap(){
        SetStatPointAdjustmentMap()
        _statPointAdjustmentMap[StatType.ATK] = 0
        _statPointAdjustmentMap[StatType.MAX_HP] = 0
    }

    func UpdateStatMap(){
        var statTypeList = GetAllKeys(_statPointMap)
        for i = 0, List.Length(statTypeList), 1{
            var statType = statTypeList[i] as int
            var statPoint = _statPointMap[statType] as int
            var stat = _statMap[statType] as int
            AdjustStatByAmount(statType, statPoint, false)
            _remainingStatPoint -= statPoint
        }
    }

    func SetStatPointMap(){
        if(_statPointMap == nil){
            ResetStatPointMap()
        }
    }

    func ResetStatPointMap(){
        _statPointMap = Map<int, int>{}
        _statPointMap[StatType.ATK] = 0
        _statPointMap[StatType.MAX_HP] = 0

    }

    func GetRemainingStatPoint() int{
        return _remainingStatPoint
    }

    func UpdateRemainingStatPoint(){
        var lvl = thisEntity<PlayerLevel>.GetLvl()
        var statTypeList = GetAllKeys(_statPointMap)
        var statPointUsed = 0
        for i = 0, List.Length(statTypeList), 1{
            var statType = statTypeList[i] as int
            statPointUsed += _statPointMap[statType] as int
        }

        _remainingStatPoint = lvl - statPointUsed
        LogInfo("[PlayerStat] _remainingStatPoint: " + _remainingStatPoint)
    }

    func AdjustStatPoint(statType int, amount int, isDecreasing bool){
        SetStatPointMap()

        LogInfo("[PlayerStat] StatPointmap before: " + _statPointMap + " " + _remainingStatPoint + " " + amount)
        if(_remainingStatPoint < amount){
            LogInfo("[PlayerStat] Not enough remaining stat point")
            return
        }

        var mult = 1
        if(isDecreasing){
            mult = -1
        }
        var currentStatPoint = _statPointMap[statType]
        _statPointMap[statType] = currentStatPoint + mult * amount
        AdjustStatByPoint(statType, amount, isDecreasing)
        _remainingStatPoint += (-1) * mult * amount
        LogInfo("[PlayerStat] statPointMap: " + _statPointMap)
    }

    func AdjustStatPointAdjustmentMap(statType int, amount int, isDecreasing bool){
        LogInfo("[PlayerStat] adjustadjust: " + statType + " " + amount + " " + isDecreasing + " " + _adjustingRemainingStatPoint + " " + _remainingStatPoint)
        SetStatPointAdjustmentMap()
        if(_adjustingRemainingStatPoint < amount && !isDecreasing){
            LogInfo("[PlayerStat] Not enough remaining stat point")
            return
        }

        if((_adjustingRemainingStatPoint + amount) > _remainingStatPoint && isDecreasing){
            LogInfo("[PlayerStat] No more statPoint")
            return
        }

        var mult = 1
        if(isDecreasing){
            mult = -1
        }

        var currentStatPointAdjustment = _statPointAdjustmentMap[statType]
        _statPointAdjustmentMap[statType] = currentStatPointAdjustment + mult * amount
        _adjustingRemainingStatPoint += (-1) * mult * amount
        LogInfo("[PlayerStat] adjustadjust: " + statType + " " + amount + " " + isDecreasing + " " + _adjustingRemainingStatPoint + " " + _remainingStatPoint)
    }
    
    func AdjustStatByPoint(statType int, statPointAmount int, isDecreasing bool){
        SetStatMap()

        var mult = 1
        if(isDecreasing){
            mult = -1 
        }

        if(statType == StatType.ATK){
            var currentATK = _statMap[StatType.ATK]
            _statMap[StatType.ATK] = currentATK + mult * statPointAmount
            thisEntity<Player>.ATK = _statMap[StatType.ATK]
            LogInfo("[PlayerStat] atk increase by: " + statPointAmount + ", totaling: " + _statMap[StatType.ATK])
            return 
        }

        if(statType == StatType.MAX_HP){
            var currentMaxHP = _statMap[StatType.MAX_HP]
            _statMap[StatType.MAX_HP] = currentMaxHP + mult * statPointAmount * 10
            thisEntity<Player>.HPMAX = _statMap[StatType.MAX_HP]
            thisEntity<Player>.HP += mult * statPointAmount * 10
            thisEntity<PlayerDefaultHUD>.UpdatePlayerHeadHUD()
            LogInfo("[PlayerStat] max hp increase by: " + statPointAmount * 10 + ", totaling: " + _statMap[StatType.MAX_HP])
            return
        }

        var statTypeExceptionList = List<int>{StatType.KILL_COUNT}

        if(Contains(statTypeExceptionList, statType)){
            LogInfo("[PlayerStat] Increasing stat through point could not be applied")
            return
        }
    }

    func AdjustStatByAmount(statType int, statAmount int, isDecrease bool){
        SetStatMap()

        var mult = 1
        if(isDecrease){
            mult = -1
        }
        if(statType == StatType.ATK){
            var currentATK = _statMap[StatType.ATK]
            _statMap[StatType.ATK] += mult * statAmount
            thisEntity<Player>.ATK = _statMap[StatType.ATK]
            LogInfo("[PlayerStat] atk increase by: " + statAmount + ", totaling: " + _statMap[StatType.ATK])
            return 
        }

        if(statType == StatType.MAX_HP){
            var currentMaxHP = _statMap[StatType.MAX_HP]
            _statMap[StatType.MAX_HP] = currentMaxHP + mult * statAmount
            thisEntity<Player>.HPMAX = _statMap[StatType.MAX_HP]
            thisEntity<Player>.HP += mult * statAmount
            thisEntity<PlayerDefaultHUD>.UpdatePlayerHeadHUD()
            LogInfo("[PlayerStat] max hp increase by: " + statAmount + ", totaling: " + _statMap[StatType.MAX_HP])
            return
        }

        if(statType == StatType.KILL_COUNT){
            var currentKillCount = _statMap[StatType.KILL_COUNT]
            _statMap[StatType.KILL_COUNT] = currentKillCount + mult * statAmount
            LogInfo("[PlayerStat] killcount increase by: " + statAmount + ", totaling: " + _statMap[StatType.KILL_COUNT])
            return
        }

        var statTypeExceptionList = List<int>{}

        if(Contains(statTypeExceptionList, statType)){
            LogInfo("[PlayerStat] Increasing stat through point could not be applied")
            return
        }
    }

    func ConfirmStatAdjustment(){
        var statTypeList = GetAllKeys(_statPointAdjustmentMap)
        for i = 0, List.Length(statTypeList), 1{
            var statType = statTypeList[i] as int
            var statPointAdjustment = _statPointAdjustmentMap[statType] as int
            // LogInfo("[PlayerStat] statPointAdjustment: " + statPointAdjustment + " " + statType)
            AdjustStatPoint(statType, statPointAdjustment, false)
        }
        LogInfo("[PlayerStat] statPointMap: " + _statPointMap)
        ResetStatPointAdjustmentMap()
        start thisEntity<PlayerData>.WriteData()
    }

    func IsStatNotAdjusted(statType int) bool{
        if(_statPointAdjustmentMap[statType] > 0){
            return false
        }
        return true
    }

    func GetStatListFromStatMap() List<int>{
        var statList = List<int>{}
        var statKeyList = _statList
        for i = 0, List.Length(statKeyList), 1{
            var statKey = statKeyList[i] as int
            var stat = _statPointMap[statKey] as int
            Append(statList, stat)
        }

        LogInfo("[PlayerStat] statList: " + statList)
        return statList
    }

    func GetStatMapFromStatList(statPointList List<int>) Map<int, int>{
        SetStatPointMap()
        var newStatMap = Map<int, int>{}
        var keyList = _statList
        for i = 0, List.Length(keyList), 1{
            var key = keyList[i] as int
            var stat = statPointList[i] as int
            LogInfo("[PlayerStat] key: " + key + ", " + stat)
            newStatMap[key] = stat
        }
        LogInfo("[PlayerStat] newStatMap: " + newStatMap)
        return newStatMap
    }

    func GetStat(statEnum int) int{
        return _statMap[statEnum]
    }

    func GetStatPoint(statEnum int) int{
        return _statPointMap[statEnum]
    }

    event Player_FinishAction(action string, data object) {        
        if (ToString(action) == ToString(Enum.ACTION_LOAD_DATA_SUCCESS)) {
            LogInfo("[PlayerStat] LoadDataSuccess _statPoint = " + thisEntity<PlayerData>._statPointList)

            _statPointMap = GetStatMapFromStatList(thisEntity<PlayerData>._statPointList)
            
            LogInfo("[PlayerStat] _statPointMap: " + _statPointMap)
        }
    }
}